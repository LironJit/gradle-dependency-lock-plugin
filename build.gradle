/*
 * Copyright 2014-2017 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'nebula.plugin-plugin' version '6.1.1'
    id 'nebula.optional-base' version '3.1.0'
    id 'nebula.kotlin' version '1.2.10'
}
apply plugin: 'nebula.plugin-plugin'
description 'Gradle plugin to allow locking of dynamic dependency versions'

group = 'com.netflix.nebula'

contacts {
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
    'nadavcoh@gmail.com' {
        moniker 'Nadav Cohen'
        github 'nadavc'
    }
}

dependencies {
    compile 'com.netflix.nebula:nebula-gradle-interop:latest.release'
    compile 'com.netflix.nebula:gradle-scm-plugin:latest.release'
    compile 'com.netflix.nebula:gradle-metrics-plugin:5.+', optional
    compile 'com.netflix.nebula:nebula-dependency-base-plugin:0.1.0'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    testCompile 'com.netflix.nebula:gradle-git-scm-plugin:latest.release'
    testCompile 'org.ajoberstar:grgit:1.+'

    // resolution rules requires at least 2.8.1, and currently DependencyLockLauncherSpec resolution-rules integration
    // test will fail without this
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.8.+'
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/gradle-dependency-lock-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/gradle-dependency-lock-plugin.git'
    description = 'Plugin to lock dynamic dependencies'

    plugins {
        dependencyLock {
            id = 'nebula.dependency-lock'
            displayName = 'Nebula Dependency Lock plugin'
            description = 'Plugin to lock dynamic dependencies'
            tags = ['nebula', 'dependencies', 'lock']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'gradle-dependency-lock-plugin'
    }
}

// Kotlin compiles first, stubbing Java dependencies, however it can't stub Groovy and we need to call Groovy code from Kotlin. For details see:
// https://discuss.gradle.org/t/kotlin-groovy-and-java-compilation/14903/10
if (GradleVersion.version(project.gradle.gradleVersion).compareTo(GradleVersion.version('4.4.1'))) {
    compileGroovy.dependsOn = compileGroovy.taskDependencies.mutableValues - 'compileJava'
} else {
    compileGroovy.dependsOn = compileGroovy.taskDependencies.values - 'compileJava'
}
compileKotlin.dependsOn compileGroovy
compileKotlin.classpath += files(compileGroovy.destinationDir)
classes.dependsOn compileKotlin
